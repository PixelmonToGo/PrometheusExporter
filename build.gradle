buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
    }
}


plugins {
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'net.minecraftforge.gradle'

minecraft {
    mappings channel: 'snapshot', version: '20180814-1.12'
}

repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

group = 'org.stonebound.prometheusexporter'
version = '2.0.2-PTG'
description = "Prometheus Exporter for Sponge"

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

defaultTasks 'build', 'shadowJar'

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
    compile 'org.spongepowered:spongeapi:7.0.0'
    compile 'io.prometheus:simpleclient_common:0.10.0'
    compile 'io.prometheus:simpleclient_hotspot:0.10.0'
    compile 'org.eclipse.jetty:jetty-server:9.4.6.v20170531'
    compile 'javax.servlet:javax.servlet-api:4.0.0'

    testCompile "org.mockito:mockito-core:3.10.0"
    testCompile "org.junit.jupiter:junit-jupiter:5.7.2"
    testCompile "org.mockito:mockito-junit-jupiter:3.10.0"
    testCompile "org.assertj:assertj-core:3.19.0"
    testCompile "io.rest-assured:rest-assured:4.4.0"
}

shadowJar {
    relocate 'org.eclipse.jetty', 'prometheus.shadow.jetty'
    relocate 'io.prometheus', 'prometheus.shadow.prometheus'
    relocate 'javax.servlet', 'prometheus.shadow.servlet'

    dependencies {
        include dependency('io.prometheus:simpleclient_common:0.10.0')
        include dependency('io.prometheus:simpleclient:0.10.0')
        include dependency('io.prometheus:simpleclient_hotspot:0.10.0')
        include dependency('org.eclipse.jetty:jetty-server:9.4.6.v20170531')
        include dependency('org.eclipse.jetty:jetty-http:9.4.6.v20170531')
        include dependency('org.eclipse.jetty:jetty-io:9.4.6.v20170531')
        include dependency('org.eclipse.jetty:jetty-util:9.4.6.v20170531')
        include dependency('javax.servlet:javax.servlet-api:4.0.0')
    }

    exclude 'jetty-dir.css'
    exclude 'about.html'

    archiveName = "spongeprometheusexporter-${version}-plugin.jar"
}

reobf {
    shadowJar{}
}

signing {
    if (project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')) {
        sign configurations.shadow
    }
}